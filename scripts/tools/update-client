#!/usr/bin/perl -w

# --------------------------------------------------------------------
# Module:  update-client
# Description:  Contacts the master server to get updates.
#   On master clients, uploads new data files to the server for
#   download by other clients.
# Author:  Bruce Alon King
# Created:  Tue Dec 28 23:38:18 2004
# --------------------------------------------------------------------

# ####################################################################
# INITIALIZATION AND GLOBAL VARIABLES
# ####################################################################
use strict;
use Getopt::Std;

my $move            = "/bin/mv";
my $cksum           = "/usr/bin/cksum";
my $server_url      = "http://www.viewtouch.com";
my $update_url      = "/cgi-bin/update-server.cgi";
my $file_url        = "/vt_updates/";
my $local_dir       = "VIEWTOUCH_PATH/dat/";
my $updates_dir     = $local_dir . "updates/";
my $vt_config       = $local_dir . ".viewtouch_config";

my $webget = "/usr/bin/curl";
my $webgetopts = "-s";
my $webgetdlopts = "--remote-name";
if ( ! -x $webget ) {
    die "update-client:  Cannot find $webget\n";
}
my $current_ver = "VIEWTOUCH_PATH/bin/vtpos -v";

ReadConfig( $vt_config );

my $options = "dhp:";
my %opts;
getopts( $options, \%opts );
ShowHelp() if ( $opts{'h'} );

if ( $opts{'d'} ) {
    $server_url = "http://localhost";
}

# Finish up some configuration now that we've processed the
# command-line and configuration file.
$update_url = $server_url . $update_url;
$file_url = $server_url . $file_url;


# ####################################################################
# MAIN LOOP
# ####################################################################
my $files = GetFileList();
my @files = split( /\n/, $files );
foreach ( @files ) {
    my ( $ck, $size, $name ) = split( / / );
    if ( CheckFile( $name, $size, $ck ) ) {
        print "$name needs to be downloaded\n";
        GetFile( $name );
    }
}


# ####################################################################
# SUBROUTINES
# ####################################################################

# --------------------------------------------------------------------
# ShowHelp:
# --------------------------------------------------------------------
sub ShowHelp {
    print "update-client [OPTIONS]\n";
    print "OPTIONS:\n";
    print "  -d          Debug mode.\n";
    print "  -h          Show this help screen.\n";
    print "  -p <file>   Upload specified file.\n";
    print "\n";
    exit( 1 );
}

#-----------------------------------------------------------------------
# ReadConfig:
#-----------------------------------------------------------------------
sub ReadConfig {
    my ( $file ) = @_;
    if ( open( CONFIG, $file ) ) {
        while ( <CONFIG> ) {
            my ( $key, $value ) = /^([^:]+):\s+(.*)$/;
            if ( $key eq "autoupdatesource" ) {
                $server_url = $value;
            }
        }
        close( CONFIG );
    }
}

# --------------------------------------------------------------------
# GetFileList:
# --------------------------------------------------------------------
sub GetFileList {
    my $list = `$webget $webgetopts $update_url`;
    return( $list );
}

# --------------------------------------------------------------------
# CheckFile: Returns 1 if the checksums and file sizes do not match,
#   indicating that the file can be updated.  Returns 0 if the files
#   do match.
# --------------------------------------------------------------------
sub CheckFile {
    my ( $name, $size, $csum ) = @_;
    my $retval = 0;
    my $local = $local_dir . $name;
    my ( $lcsum, $lsize, $full ) = split( m/ /, `$cksum $local` );
    if ( $lcsum != $csum || $lsize != $size ) {
        $retval = 1;
    }

    return( $retval );
}

# --------------------------------------------------------------------
# GetFile:
# --------------------------------------------------------------------
sub GetFile {
    my ( $filename ) = @_;
    chdir( $updates_dir );
    system( "$webget $webgetopts $webgetdlopts $file_url/$filename" );
}

