#!/usr/bin/perl -w

# --------------------------------------------------------------------
# Module:  rotate
# Description:  rotates various log files.  Intended to prevent
#   /var/log from filling up.
# Author:  Bruce Alon King
# Created:  Tue Aug 27 09:34:47 2002
# --------------------------------------------------------------------

# ####################################################################
# INITIALIZATION AND GLOBAL VARIABLES
# ####################################################################
use strict;
use Getopt::Std;

my $options = "h";
my %opts;
getopts( $options, \%opts );
ShowHelp() if ( $opts{'h'} || ( @ARGV == 0 ) );

my $rotate_days = shift( @ARGV );
my $rotate_file = shift( @ARGV );


# ####################################################################
# MAINLOOP
# ####################################################################
my $files = CollectFiles( $rotate_file );
foreach ( @$files ) {
    my $file = $_;
    RotateFile( $file );
}


# ####################################################################
# SUBROUTINES
# ####################################################################

# --------------------------------------------------------------------
# ShowHelp:
# --------------------------------------------------------------------
sub ShowHelp {
    my ( $progname ) = $0 =~ m|/([^/]+)$|;
    print STDERR "Usage:  $progname <days> <file or directory>\n";
    print STDERR "\n";
    exit( 1 );
}

# --------------------------------------------------------------------
# CollectFiles:  If the given file is a file, just return that.  If
#   it is a directory, return all of the (non-hidden) files in that
#   directory.  Otherwise return an error.
# --------------------------------------------------------------------
sub CollectFiles {
    my ( $file ) = @_;
    my @files;
    if ( -f $file ) {
        push( @files, $file );
    } elsif ( -d $file ) {
        if ( opendir( DIR, $file ) ) {
            my @contents;
            push( @contents, readdir( DIR ) );
            foreach ( sort @contents ) {
                my $filename = $file . "/" . $_;
                next if ( -d $filename );
                next if ( $filename =~ /\.[0-9]+$/ );
                push( @files, $filename );
            }
            closedir( DIR );
        }
    }
    return( \@files );
}

# --------------------------------------------------------------------
# RotateFile:
# --------------------------------------------------------------------
sub RotateFile {
    my ( $file ) = @_;
    if ( -f $file ) {
        my $newfile = $file;
        my $extension = 1;
        if ( $newfile =~ s/\.([0-9]+)$// ) {
            $extension = $1 + 1;
        }
        $newfile .= "." . $extension;
        if ( $extension <= $rotate_days ) {
            RotateFile( $newfile ) if ( -f $newfile );
            system( "mv", $file, $newfile );
        }
    }
}
